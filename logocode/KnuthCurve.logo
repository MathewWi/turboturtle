; KnuthCurve, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/fractals/knuth.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "WindowSize 400
.setspecial "FramesPerSec 2

Forever [GoX Wait 30]

To Init :Order
  Make "Ord Round :Order/2
  Make "Size 3 * Power 2 (6-:Ord)            ; make Size a global value
End

To Hue :Angle
  Make "Red Round 127*(1+Sin :Angle)
  Make "Green Round 127*(1+Sin(120+:Angle))
  Make "Blue Round 127*(1+Sin(240+:Angle))
  Output ( List :Red :Green :Blue )
End

To Display :Order
  SetPC 7 PenUp
  SetPos [-190 184] (Label "Knuth "Curve "Order :Order)
End

To Knuth :Order :Angle :Turn
  If :Order < 1 [Right 45+:Turn Forward :Size Left 45+:Turn Stop]
  Right 2*:Turn+:Angle
  Knuth :Order-1 2*:Turn Minus :Turn
  Right 45-3*:Turn-:Angle Forward :Size Left 45-:Turn+:Angle
  Knuth :Order-1 0 Minus :Turn
  Right :Angle
End

To Go :Order
  PenUp Init :Order
  SetPos [-190 -184]
  SetPC 6 PenDown
  Knuth :Order -90 45             ; angle -90 turn 45
End

To GoX
  PenUp
  For [Order 2 16 2] [            ; make [Order 1 10 2] for odd curves
  Init :Order Display :Order
  SetPenSize 4-(:Order/4) SetPC Hue :Order*22.5 + 45
  PenUp SetPos [-190 -184] PenDown
  Knuth :Order -90 45 ClearScreen]
End 

