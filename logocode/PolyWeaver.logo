; Poly Weaver, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/wand/weaver.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400
.setspecial "framespersec 40

Go

To Go
  Forever [
    Init
      For [OffSet 0 480 2] [
      PenUp SetXY -:Side/2 -60 PenDown
      Weave :PolyA :PolyB :OffSet
      Display :PolyA :PolyB
      ClearScreen]
    Wait 120]
End

To Display :PolyA :PolyB
  ; write header title and footer values
  PenUp SetPC 7 SetHeading 0 SetJustifyVert 1
  SetPos [-190 182] (Label "PolyWeaver)
  SetPos [-190 -190] (Label "A "B "= :PolyA :PolyB)
  SetPos [134 -190] (Label "Rep "= :Rep)
End

To Init
  Make "Side 52
  Make "PolyA Pick [3 4 5 6 7 8 9 10 11 12]
  Make "PolyB Pick [2 3 4 5 6 7 8]
  Make "Rep HCF :PolyA :PolyB
  Make "Rep (:PolyA/:Rep) * :PolyB
End

To HCF :A :B
  ; Output the highest common factor of two input numbers
  Make "Rem Remainder :A :B
  IfElse :Rem = 0 [ Output :B ] [ Output HCF :B :Rem ]
End

To Weave :PolyA :PolyB :OffSet
  Make "A 90 Make "B 90
  Repeat :Rep [
    SetHeading :A
    SetPC 2 Forward :Side
    SetHeading :B + :OffSet + :A
    SetPC 6 Forward :Side
    Make "A :A-(360/:PolyA)
    Make "B :B-(360/:PolyB) ]
End


