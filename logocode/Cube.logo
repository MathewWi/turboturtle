; Cube, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/anim/cube.html

.setspecial "windowsize 400
.setspecial "framespersec 100

New 3DShape Init Draw

To New
  SetBG 0 SetPC 2 PenUp
End

To Init
  Make "AngleX 0
  Make "AngleY 0
  Make "AngleZ 0
  Make "M Count :Color
End

To Draw
  Forever [
  Make "N 1
  Make "AngleX :AngleX+0.5
  Make "AngleY :AngleY+0.7
  Make "AngleZ :AngleZ+0.3
  Repeat :M [
  Make "Xd Item :N :ShapeX
  Make "Yd Item :N :ShapeY
  Make "Zd Item :N :ShapeZ
  Make "Zx :Xd*(Cos :AngleZ) - :Yd*(Sin :AngleZ) - :Xd
  Make "Zy :Xd*(Sin :AngleZ) + :Yd*(Cos :AngleZ) - :Yd
  Make "Yx (:Xd+:Zx)*(Cos :AngleY) - :Zd*(Sin :AngleY) - (:Xd+:Zx)
  Make "Yz (:Xd+:Zx)*(Sin :AngleY) + :Zd*(Cos :AngleY) - :Zd
  Make "Xy (:Yd+:Zy)*(Cos :AngleX) - (:Zd+:Yz)*(Sin :AngleX) - (:Yd+:Zy)
  Make "Xz (:Yd+:Zy)*(Sin :AngleX) + (:Zd+:Yz)*(Cos :AngleX) - (:Zd+:Yz)
  Make "Z :Zd + (:Xz+:Yz)
  Make "X (:Xd + (:Yx+:Zx))*((:Z+300)/295)
  Make "Y (:Yd + (:Zy+:Xy))*((:Z+300)/295)
  SetPos List :X :Y
  SetPC Item :N :Color Circle 4+(:Z+200)/50 Make "N :N+1]
  ClearScreen ]
End

To 3DShape
  ; cube
  Make "ShapeX [80 -80 -80 80 80 -80 -80 80]
  Make "ShapeY [80 80 -80 -80 -80 -80 80 80]
  Make "ShapeZ [80 80 80 80 -80 -80 -80 -80]
  Make "Color [1 2 4 6 7 11 14 15]
End

To Circle :radius
  PenUp
  make "step 2 * :radius * sin 6
  fd :radius rt 90
  PenDown repeat 30 [ fd :step rt 12 ]
  PenUp lt 90 bk :radius
End
