; Tree Generator, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/trees/tree3.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400

Forever [New Back 160 PenDown Tree 80 ClearScreen Wait 200]

To New
  SetBG 0 SetPC 6 PenUp
End

To TreeCol :Length
  Make "Green Round 2.5*(100-:Length)        ; green depends on length
  Output ( List 255 :Green 0 )               ; red set to 255 and blue to 0
End

To Tree :Length
  If :Length< 10 [Blossom Stop]         ; ends recursion if branch too small
  SetPenSize :Length/9                  ; reduce pen width as branch length gets smaller
  SetPC TreeCol :Length                 ; branch color depends on length
  LocalMake "Angle Pick [24 32 40 48 56 64 72]
  LocalMake "TreeAngle :Angle*Pick [0.25 0.375 0.5 0.625 0.75]
  Forward :Length
  Left :TreeAngle
  Tree :Length * Pick [0.7 0.75 0.8] Right :Angle
  Tree :Length * Pick [0.7 0.75 0.8] Left :Angle
  Right :TreeAngle
  PenUp Back :Length PenDown        ; return to starting point
End

To Blossom
  Make "myHeading Heading
  SetPC 2 SetHeading 180
  Forward 5 Back 5 SetHeading :myHeading
End

