; Sierpinsky Triangle Curve, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/fractals/stc.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400
.setspecial "framespersec 0.7

Forever [ Repeat 12 [ Go RepCount ClearScreen ] Wait 60 ]

To Init
  ; make Size global value
  Make "Size 384
  PenUp SetPos [-192 -168] SetH 90
End

To Display :Order
  ; write header title, curve depth and info
  SetPC 7 SetJustifyHorz 0 SetJustifyVert 1 PenUp
  SetPos [-190 180] (Label "Sierpinski "Triangle "Curve)
  SetJustifyHorz 2
  SetPos [190 180] (Label "Order: :Order)
End

To Sierpinski :Order :Size :Parity
  If :Order < 1 [Forward :Size Stop]       ; ie if = 0
  Left :Parity*60
  Sierpinski :Order-1 :Size/2 Minus :Parity
  Right :Parity*60
  Sierpinski :Order-1 :Size/2 :Parity
  Right :Parity*60
  Sierpinski :Order-1 :Size/2 Minus :Parity
  Left :Parity*60
End

To Go :Order
  Display :Order Init
  PenDown Sierpinski :Order :Size 1
End

