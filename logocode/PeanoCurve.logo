; Peano Curve, from http://www.s193653721.websitehome.co.uk/logoarts/picts/gallery1/peano.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400

Curves

To Curves
  For [Level 0 2]
  [SetPC Item :Level+1 [6 4 1] SetPenSize 10-(4*:Level)
  Init :Level Curve :Level]
End 

To Init :Level
  Make "CellSize (Power 2 0.5)*(120/(Power 3 :Level))
  Make "Size :CellSize*0.6
  Make "TurnSize :CellSize*0.283
  PenUp SetPos [-180 -180] SetHeading 45
  Forward :CellSize*0.2 PenDown
End

To Curve :Level
  If :Level < 0 [Forward :Size Stop]
  Curve :Level-1
  LeftTurn Curve :Level-1
  Repeat 3 [RiteTurn Curve :Level-1]
  Repeat 3 [LeftTurn Curve :Level-1]
  RiteTurn Curve :Level-1
End

To LeftTurn
  Left 45 Forward :TurnSize Left 45
End

To RiteTurn
  Right 45 Forward :TurnSize Right 45
End 
