; String, from http://www.s193653721.websitehome.co.uk/logoarts/picts/gallery1/string.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400
.setspecial "framespersec 0.333

setpensize 1.5
forever [Init Loom :StepA :StepB :Offset cs]

To Init
  Make "StepA Pick [0 2 3 4]
  Make "StepB Pick [-3 -2 1 2 3 4]
  Make "Offset 15*Random 9
  PenUp SetXY -198 198 SetPC 7 (Label "Loom :StepA :StepB :Offset)
End

To Loom :StepA :StepB :Offset
  For [Angle 0 360 2] [
    SetPC AngCol :Angle PenUp
    SetXY 180*Sin :Angle 180*Cos :Angle PenDown  ; Start point on circle
    SetXY 180*Sin (:StepA*:Angle) 180*Cos (:Offset+(:StepB*:Angle)) ; Finish point
    ]
End

To AngCol :Ang
  ; Output list of red, grn, blue
  Make "Red 127*(1+Cos :Ang)
  Make "Grn 127*(1+Cos(120+:Ang))
  Make "Blue 127*(1+Cos(240+:Ang))
  Output ( List :Red :Grn :Blue )
End

