; DragonCurve, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/fractals/dragon.html

.setspecial "windowsize 400
.setspecial "FramesPerSec 7

Forever [GoX Wait 45]

To Init :Order
  ; make Size and Angle global values
  Make "Size 11*Power (Sqrt 2) (9-:Order)
  Make "Angle 90
End

To Display :Order
    ; write header title and curve level
  SetPC 7
  SetPos [-190 184] (Label "Dragon "Curve "Order :Order)
End

To Dragon :Order :Parity
  If :Order < 1 [Forward :Size Stop]      ; ie if = 0
  Dragon :Order-1 1
  Left :Parity*:Angle            ; turn left or right :Angle degrees
  Dragon :Order-1 Minus 1
End

To Go :Order
  PenUp
  Init :Order Display :Order
  SetPos [-108 0] SetPC 2 PenDown
  Right (45 *:Order)+90           ; similar orientations of curves
  Dragon :Order 1
End

To GoX
  PenUp
  For [Order 0 20] [
  Init :Order Display :Order
  SetPos [-108 0] SetPC 6
  ; SetPC 1 + Modulo :Order 2            ; red or green
  PenDown Right (45 *:Order)+90           ; similar orientations of curves
  Dragon :Order 1
  ClearScreen PenUp]
End

