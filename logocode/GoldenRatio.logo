; Golden Ratio, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/spirals/goldenspiral.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400

New Lines SetPos [-185 -140]
Section 228         ; begin with square of side 228

To New
  ; set defaults, screen, pen and turtle
  SetBG 0 SetPC 2 PenUp
End

To Lines
  SetPC [0 0 128]         ; draw crossed lines in blue
  PenUp SetPos [-185 88]
  PenDown SetPos [184 -140]
  PenUp SetPos [184 88]
  PenDown SetPos [43 -140]
End

To Section :Side
  If :Side < 2 [Stop]         ; stop when side too short
  SetPC 2 Square :Side         ; comment out for curve only
  SetPC 4 Curve :Side         ; comment out for squares only
  Section :Side*0.618        ; do again with smaller side
End

To Square :Side
  Repeat 4 [Forward :Side Right 90]
End

To Curve :Side
  Make "Step :Side*0.0524        ; heading correction pi/60
  Right 1.5
  Repeat 30 [Forward :Step Right 3]         ; 90 degree arc in 30 steps
  Left 1.5
End


