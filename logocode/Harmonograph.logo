; Harmonograph, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/spirograph/harm.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400

Forever [New Init Display Spir ClearScreen Wait 200]

To New
  SetBG 0 SetPC 2 PenUp
End

To Spir
  SetPos XYCurve 0 PenDown
  For [T 0 5000] [SetPC AngCol :T SetPos XYCurve :T]
End

To XYCurve :T
  Make "KK :K * Power :e ( Minus 0.00015 * :T )
  Make "X :KK*((Cos ((:f1*:T) + :p1 )) + (Cos ((:f2*:T) + :p2 )))
  Make "Y :KK*((Cos ((:f3*:T) + :p3 )) + (Cos ((:f4*:T) + :p4 )))
  Output (List :X :Y)
End

To Init
  Make "K 90
  Make "e 2.718
  Make "f1 1+ Random 9
  Make "f2 1+ Random 9
  Make "f3 1+ Random 9
  Make "f4 1+ Random 9
  Make "p1 Random 360
  Make "p2 Random 360
  Make "p3 Random 360
  Make "p4 Random 360
End

To Display
  ; write header title and footer values
  SetPC 7 SetJustifyVert 1
  SetPos [-190 184] (Label "Harmonograph)
  SetPos [-190 -190] (Label "Freqs: :f1 :f2 :f3 :f4)
  SetPos [34 -190] (Label "Phase: :p1 :p2 :p3 :p4)
End

To AngCol :Theta
  Make "Red Abs 255 *Cos (:Theta)
  Make "Gre Abs 255 *Cos (:Theta + 120)
  Make "Blu Abs 255 *Cos (:Theta + 240)
  Output (List :Red :Gre :Blu)
End

