; Spiral Cirles, based on Spiral Squares
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400
.setspecial "framespersec 40

Init Forever [ PenUp SetPos [-46 -210] SetHeading -30 Circle :vsize :vangle :vratio Advance Clearscreen ]

To Circle :Size :Angle :Ratio
  If :Size < 1 [Stop]         ; stop when size too small
  PenDown Repeat 60 [Forward :Size/15 Left 6] ; draw a circle
  PenUp Forward :Size Right :Angle
  Circle :Size*:Ratio :Angle :Ratio        ; do again with smaller size
End

to Advance
  make "vsize :vsize + :dsize
  make "vangle :vangle + :dangle
  make "vratio :vratio + :dratio
  if or :vsize < 80 :vsize > 110 [make "dsize -:dsize]
  if or :vangle < 20 :vangle > 40 [make "dangle -:dangle]
  if or :vratio < 0.8 :vratio > 0.92 [make "dratio -:dratio]
end

To Init
  SetPC 7
  PenDown
  make "vsize 92
  make "vangle 30
  make "vratio 0.9
  make "dsize (0.02 + 0.04 * (0.01 * random 100)) * ((2 * random 2) - 1)
  make "dangle (0.03 + 0.05 * (0.01 * random 100)) * ((2 * random 2) - 1)
  make "dratio 0
End

