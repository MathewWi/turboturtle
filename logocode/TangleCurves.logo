; Tangle Curves, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/wand/wandtangle.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400

; draw random tangle curves
New Forever [Tangle Random 2048 ClearScreen Wait 88]

To New
  SetBG 0 SetPC 6 SetPenSize 2.0 PenUp
End

To Tangle :N
  Make "LengthNew 16
  Fit2Screen :N :LengthNew        ; comment out to skip curve optimisation
  ;EraseScreen 
  Display :N :Xcent :Ycent :LengthNew
  SetXY :Xcent :Ycent PenDown
  For [I 0 :N] [
  SetPC AngCol :I :N
  SetHeading Function :I :N Forward :LengthNew]
  PenUp
End

To Fit2Screen :N :Length
  Home
  Make "Xmax 0 Make "Xmin 0
  Make "Ymax 0 Make "Ymin 0
  PenUp For [I 0 :N] [
  SetHeading Function :I :N Forward :Length
  If xcor > :Xmax [Make "Xmax xcor]
  If xcor < :Xmin [Make "Xmin xcor]
  If ycor > :Ymax [Make "Ymax ycor]
  If ycor < :Ymin [Make "Ymin ycor] ]
  Make "Xsize :Xmax + Abs :Xmin
  Make "Ysize :Ymax + Abs :Ymin
  IfElse :Xsize > :Ysize [Make "Size :Xsize] [Make "Size :Ysize]
  Make "Scale 360/:Size
  Make "LengthNew :Length*:Scale
  If (Abs :Scale - 1) > 0.1 [ home Fit2Screen :N :LengthNew stop ]
  Make "Xcent Minus (:Xmax + :Xmin)/2
  Make "Ycent Minus (:Ymax + :Ymin)/2
End

To Function :I :N
  Output (360*(Power :I 3)/:N)-90
End

To Display :N :Xcent :Ycent :LengthNew
  ; write header and footer info
  SetHeading 0 SetPC 7 SetJustifyVert 1
  SetPos [-190 184] (Label "Tangle "Curve)
  SetPos [150 184] (Label :N)
  SetPos [-190 -190] (Label Round :Xcent Round :Ycent)
  SetPos [150 -190] (Label Round :LengthNew)
End

To AngCol :I :N
  Make "Green Round 255*(Sin 180*:I/:N)
  Output (List 255 :Green 0)
End

