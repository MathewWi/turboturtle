; Fractal Fern, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/dotplots/fern2.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400
.setspecial "framespersec 30

SetPenSize 0.75
Forever [ Go ClearScreen ]

To Fern
  If :Prob < 701 [       ; 0 to 700 = 70.1%
    Map 0.81 0.07 0.12 Minus 0.04 0.84 0.195 Stop]
  If :Prob < 851 [       ; 701 to 850 = 15%
    Map 0.18 Minus 0.25 0.12 0.27 0.23 0.02 Stop]
  If :Prob < 980 [       ; 851 to 979 = 12.9%
    Map 0.19 0.275 0.16 0.238 Minus 0.14 0.12 Stop]
  ; Else 980 to 999 = 2%
    Map 0.0235 0.087 0.11 0.045 0.1666 0
End

To Map :a :b :c :d :e :f
  Make "Xnew (:a*:X) + (:b*:Y) + :c
  Make "Y (:d*:X) + (:e*:Y) + :f
  Make "X :Xnew
End

To Scale :XorY
  ; XorY are in the range 0 to 1, we need -200 to 200
  Output (400*:XorY)-200
End

To Go
  Make "X 0 Make "Y 0        ; Global variables
  Repeat 25000 [
    Make "Prob Random 1000
    Fern
    Dot Scale :X Scale :Y ]
End

