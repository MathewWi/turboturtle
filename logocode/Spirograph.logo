; Spirograph, from: http://www.s193653721.websitehome.co.uk/logoarts/progs/spirograph/spirog.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400
.setspecial "framespersec 0.4

Forever [Rand2 Spir ClearScreen]

To Spir
  SetPC 7 PenUp
  SetPos [-190 200] SetJustifyVert 0 (Label "Spirograph)
  SetPos [-190 -200] SetJustifyVert 2 (Label "A= :A "B= :B)
  For [S 10 70 5] [
  PenUp SetPos XYCurve :S 0 PenDown SetPC AngCol :S+10*:B
  For [T 1 360] [SetPos XYCurve :S :T]]
End

To XYCurve :S :T
  Make "X 120 * (Sin :S+:A*:T) + :S * (Cos (:B*:T))
  Make "Y 120 * (Cos :S+:A*:T) + :S * (Sin (:B*:T))
  Output (List :X :Y)
End

To Rand2
  Make "A (Pick [-1 1]) * Pick [1 2]
  Make "B (Pick [-1 1]) * Pick [2 3 4 5 6]
End

To AngCol :Theta
  Make "Red Abs 255 *Cos (:Theta)
  Make "Gre Abs 255 *Cos (:Theta + 120)
  Make "Blu Abs 255 *Cos (:Theta + 240)
  Output ( List :Red :Gre :Blu )
End

