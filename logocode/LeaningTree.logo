; Leaning Tree, from http://www.s193653721.websitehome.co.uk/logoarts/progs/trees/tree1.html
; Copyright (C) 2005-2008 Guy Walker
; Copyright (C) 2009 Richard Goedeken (Richard@fascinationsoftware.com)

; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, version 3.

; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.

; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.setspecial "windowsize 400
.setspecial "framespersec 30

Go

To Tree :Length
  If :Length < 5 [Stop]                  ; ends recursion if branch length too small
  SetPenSize :Length/9                   ; reduce pen width as branch length gets smaller
  SetPC TreeCol :Length                  ; branch color depends on length
  PenDown Forward :Length Left :Angle/2
  Tree :Length*0.65 Right :Angle         ; short branch length
  Tree :Length*0.85 Left :Angle/2        ; long branch length
  PenUp Back :Length                     ; return to starting point
End

To TreeCol :Length
  Make "Green Round 2.5*(100-:Length)    ; green depends on length
  Output ( List 255 :Green 0 )           ; red set to 255 and blue to 0
End

To Go
  PenUp SetPos [-94 -160] Right 5
  Make "Angle 50 Tree 85                 ; initial angle of 50 shown in red
End
